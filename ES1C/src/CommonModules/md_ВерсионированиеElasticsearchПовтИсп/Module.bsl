////////////////////////////////////////////////////////////////////////////////
// Интеграция elasticsearch (сервер, повторное использование)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция НастройкиРазмещенияПоТипу(ТипМетаданных, ВидФормы) Экспорт
	
	НастройкиРазмещения = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипМетаданных", ТипМетаданных);
	Запрос.УстановитьПараметр("ВидФормы", ВидФормы);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВсеНастройкиСравнения.Настройка КАК Настройка,
		|	ЕстьNull(НастройкаСравнения.Ключ, """") КАК ИмяКоманды,
		|	ЕстьNull(НастройкаСравнения.Представление, """") КАК ЗаголовокКнопкиФормы,
		|	ЕстьNull(md_ИндексыElasticsearch.Имя, """") КАК ИмяИндекса,
		|	ЕстьNull(md_СервераElasticsearch.АдресСервера, """") КАК АдресСервера,
		|	ЕстьNull(md_СервераElasticsearch.Порт, """") КАК ПортСервера,
		|	ЕстьNull(md_СервераElasticsearch.Ссылка, Значение(Справочник.md_СервераElasticsearch.ПустаяСсылка)) КАК СерверES,
		|	ЕстьNull(md_ИндексыElasticsearch.Ссылка, Значение(Справочник.md_ИндексыElasticsearch.ПустаяСсылка)) КАК ИндексES
		|
		|ИЗ
		|	РегистрСведений.md_НастройкиРазмещенияОтчетаСравнениеВерсий КАК ВсеНастройкиСравнения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.md_НастройкиСравненияВерсий КАК НастройкаСравнения
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.md_ИндексыElasticsearch КАК md_ИндексыElasticsearch
		|			ПО НастройкаСравнения.ИндексES = md_ИндексыElasticsearch.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.md_СервераElasticsearch КАК md_СервераElasticsearch
		|			ПО НастройкаСравнения.СерверES = md_СервераElasticsearch.Ссылка
		|		ПО ВсеНастройкиСравнения.Настройка = НастройкаСравнения.Ссылка
		|ГДЕ
		|	ВсеНастройкиСравнения.ТипМетаданных = &ТипМетаданных
		|	И ВсеНастройкиСравнения.ВидФормы = &ВидФормы
		|	И ВсеНастройкиСравнения.Используется";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат НастройкиРазмещения;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НастройкаРазмещения = Новый Структура("ИмяКоманды, ЗаголовокКнопкиФормы");
		ЗаполнитьЗначенияСвойств(НастройкаРазмещения, Выборка);
		
		ПараметрыПодключения = Новый Структура("ИмяИндекса, АдресСервера, ПортСервера, СерверES, ИндексES");
		ЗаполнитьЗначенияСвойств(ПараметрыПодключения, Выборка);
		
		НастройкаРазмещения.Вставить("ПараметрыПодключения", ПараметрыПодключения);
		
		НастройкиРазмещения.Добавить(НастройкаРазмещения);
		
	КонецЦикла;
	
	Возврат НастройкиРазмещения;
	
КонецФункции

#КонецОбласти