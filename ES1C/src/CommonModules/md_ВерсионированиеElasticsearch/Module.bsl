////////////////////////////////////////////////////////////////////////////////
// Интеграция elasticsearch (сервер)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик подписки на событие ПриЗаписи для фиксации версии объекта, версионируемой в Elasticsearch.
//
// Параметры:
//  Источник   - ДокументОбъект - Объекты документов, версионируемых в Elasticsearch.
//  Отказ      - Булево - параметр, передаваемый в подписку на событие ПриЗаписи.
//
Процедура ВерсионированиеElasticsearch_ДокументОбъект_ПриЗаписи(Источник, Отказ) Экспорт
	
    Если Отказ ИЛИ НЕ md_ВерсионированиеElasticsearchВызовСервера.ИспользуетсяИнтеграцияСElasticsearch() Тогда
        
        Возврат;
        
    КонецЕсли; 
    
	ТипМетаданных = Источник.Метаданные().ПолноеИмя();	
	ПараметрыВерсионирования = ПараметрыВерсионированияВElasticSearch(ТипМетаданных);
	                    
	Если НЕ ПараметрыВерсионирования.Количество() = 0 Тогда
        
        АвторизированныйПользователь = md_ОбщегоНазначения.ТекущийПользователь();
        УниверсальноеВремяНаСервере = md_ОбщегоНазначенияElasticsearch.УниверсальноеВремяНаСервере();
        
		СтруктураОбработкиES = Новый Структура;
		СтруктураОбработкиES.Вставить("ТипМетаданных", ТипМетаданных);
		СтруктураОбработкиES.Вставить("ПараметрыВерсионирования", ПараметрыВерсионирования);
        // чтобы ES воспринял данные как дату нужно скормить ему ее в формате 2018-12-11T11:26:50Z
        // можно формировать вручную, либо воспользоваться встроенным методом ЗаписатьДатуJSON
		СтруктураОбработкиES.Вставить("ДатаРегистрации", УниверсальноеВремяНаСервере);
		СтруктураОбработкиES.Вставить("СсылкаНаОбъект", Источник.Ссылка);        
        СтруктураОбработкиES.Вставить("АвторВерсииПоДанным1С", АвторизированныйПользователь);
        		
		ПодготовитьСтруктуруХраненияДокументаВES(Источник, СтруктураОбработкиES);
		ПоставитьДокументВОчередьОтправкиВElasticSearch(СтруктураОбработкиES);
		
	КонецЕсли; 	

КонецПроцедуры


Функция ОбъектВерсионируетсяВElasticSearch(ТипМетаданных) Экспорт
	
	Возврат md_ВерсионированиеElasticsearchВызовСервера.ОбъектВерсионируетсяВElasticSearch(ТипМетаданных);
		
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область РегламентныеЗадания

// См. процедуру md_ИнтеграцияElasticsearchВызовСервера.ОтправитьДанныеОчередейВElasticsearch
Процедура md_ОтправкаДанныхОчередейВElasticsearch() Экспорт
	
    Если НЕ md_ВерсионированиеElasticsearchВызовСервера.ИспользуетсяИнтеграцияСElasticsearch() Тогда
        
        Возврат;
        
    КонецЕсли; 
    
    md_ВерсионированиеElasticsearchВызовСервера.ОтправитьДанныеОчередейВElasticsearch();	
	
КонецПроцедуры

// См. процедуру md_ИнтеграцияElasticsearchВызовСервера.ОтправитьДанныеОчередейВElasticsearch
Процедура md_ОчисткаЖурналовСообщенийElasticsearch() Экспорт
    
    Если НЕ md_ВерсионированиеElasticsearchВызовСервера.ИспользуетсяИнтеграцияСElasticsearch() Тогда
        
        Возврат;
        
    КонецЕсли; 

    md_ВерсионированиеElasticsearchВызовСервера.ОчиститьЖурналСообщенийElasticsearch();
    
КонецПроцедуры

#КонецОбласти 

#Область ОтчетСравнениеВерсий

Процедура ФормаПриСозданииНаСервере(Форма, Отказ) Экспорт
	
	ЭтоФормаОбъекта = Истина;
	Параметры = Форма.Параметры;
	ЕстьПараметрыСписка  = Параметры.Свойство("Отбор") И Параметры.Свойство("ТекущаяСтрока");
	ЕстьПараметрыОбъекта = Параметры.Свойство("Ключ")  И Параметры.Свойство("Основание");
	
	Если НЕ ЕстьПараметрыСписка = ЕстьПараметрыОбъекта Тогда
		
		ЭтоФормаОбъекта = ЕстьПараметрыОбъекта;
	
	КонецЕсли;
	
	ТипМетаданных = ИмяВладельцаФормы(Форма);
	
	ВидФормы = ?(ЭтоФормаОбъекта, Перечисления.md_ВидФормы.ФормаЭлемента, Перечисления.md_ВидФормы.ФормаСписка);
	
	НастройкиРазмещения = md_ВерсионированиеElasticsearchПовтИсп.НастройкиРазмещенияПоТипу(ТипМетаданных, ВидФормы);
	
	Если НастройкиРазмещения.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДобавитьХранилищеНастроекНаФорму(Форма, НастройкиРазмещения);
	
	Для Каждого НастройкаРазмещения ИЗ НастройкиРазмещения Цикл
		
		ДобавитьКомандуОтчетаНаФорму(Форма, НастройкаРазмещения);
		
	КонецЦикла;
			
КонецПроцедуры	
	
#КонецОбласти
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьХранилищеНастроекНаФорму(Форма, НастройкиРазмещения)
	
	ИмяРеквизита = "ПараметрыПодключенияКElasticsearch";
	РеквизитФормы = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов());
	
	МассивДобавляемыыхРеквизитов = Новый Массив();
	МассивДобавляемыыхРеквизитов.Добавить(РеквизитФормы);
	
	Форма.ИзменитьРеквизиты(МассивДобавляемыыхРеквизитов);
	
	ПараметрыПодключения = СформироватьПараметрыПодключенияПоНастрокам(НастройкиРазмещения);
	
	Форма[ИмяРеквизита] = Новый ХранилищеЗначения(ПараметрыПодключения);
	
КонецПроцедуры

Функция СформироватьПараметрыПодключенияПоНастрокам(НастройкиРазмещения)
	
	ПараметрыПодключения = Новый Соответствие();
	
	Для Каждого НастройкаРазмещения ИЗ НастройкиРазмещения Цикл
		
		ПараметрыПодключения.Вставить(НастройкаРазмещения.ИмяКоманды, НастройкаРазмещения.ПараметрыПодключения);	
		
	КонецЦикла;
	
	Возврат ПараметрыПодключения;

КонецФункции

Процедура ДобавитьКомандуОтчетаНаФорму(Форма, НастройкаРазмещения)

	Элементы = Форма.Элементы;
	
	КомандаСравнениеВерсий = Форма.Команды.Добавить(НастройкаРазмещения.ИмяКоманды);
	КомандаСравнениеВерсий.Действие = "Подключаемый_СравнитьВерсии";

	НовыйЭлемент = Элементы.Добавить(НастройкаРазмещения.ИмяКоманды, Тип("КнопкаФормы"), 
	Элементы.Формаmd_Версионирование);
	НовыйЭлемент.ИмяКоманды = НастройкаРазмещения.ИмяКоманды;
	НовыйЭлемент.Заголовок = НастройкаРазмещения.ЗаголовокКнопкиФормы;
	
КонецПроцедуры

Функция ИмяВладельцаФормы(Форма)
	
	ИмяВладельца = "";
	ИмяФормы = Форма.ИмяФормы;
	
	МассивПутей = СтрРазделить(ИмяФормы, ".");	
	
	Если МассивПутей.Количество() < 2 Тогда
		
		Возврат ИмяВладельца;
			
	КонецЕсли;
	
	Корень = СокрЛП(НРег(МассивПутей[0]));
	
	Если НЕ (Корень = "документ" ИЛИ Корень = "справочник") Тогда
		
		Возврат ИмяВладельца;
		
	Конецесли;
	
	МассивИмениОбъекта = Новый Массив(2);
	
	Для i = 0 По МассивИмениОбъекта.Количество() - 1 Цикл
	
		МассивИмениОбъекта[i] = МассивПутей[i];	
		
	КонецЦикла;
	
	ИмяВладельца = СтрСоединить(МассивИмениОбъекта, ".");
	
	Возврат ИмяВладельца;
	
КонецФункции
 
Процедура ПоставитьДокументВОчередьОтправкиВElasticSearch(СтруктураОбработкиES)
	
	md_ВерсионированиеElasticsearchВызовСервера.ПоставитьДокументВОчередьОтправкиВElasticSearch(СтруктураОбработкиES);	
	
КонецПроцедуры

Функция ПараметрыВерсионированияВElasticSearch(ТипМетаданных) Экспорт
	
	Возврат md_ВерсионированиеElasticsearchВызовСервера.ПараметрыВерсионированияВElasticSearch(ТипМетаданных);
		
КонецФункции

// Подготавливает сериализованные данные объекта для записи в регистр сведений md_ОчередьДанныхКОтправкеВElasticsearch
//
// Параметры:
//  Объект   - ДокументОбъект - объект документа, версионируемого с Elasticsearch 
//  СтруктураОбработкиES - Структура - структура, в которую будут помещены данные для записи 
//  в регистр сведений md_ОчередьДанныхКОтправкеВElasticsearch
//
Процедура ПодготовитьСтруктуруХраненияДокументаВES(Объект, СтруктураОбработкиES)
    
	МетаданныеОбъекта = Объект.Метаданные();
	
    ПредставлениеСсылки = md_ВерсионированиеElasticsearchСлужебный.ПолучитьПредставлениеСсылкиДляES(Объект.Ссылка);
    ПредставлениеПользователя = Строка(ПользователиИнформационнойБазы.ТекущийПользователь());
    ДатаРегистрации = Формат(СтруктураОбработкиES.ДатаРегистрации, "ДФ=yyyy-MM-ddTHH:mm:ssZ");
    ПредставлениеПользователя1С = 
    md_ВерсионированиеElasticsearchСлужебный.ПолучитьПредставлениеСсылкиДляES(СтруктураОбработкиES.АвторВерсииПоДанным1С);
    ИмяСервераСеанса =  md_ОбщегоНазначенияElasticsearch.ИмяСервераСеанса();
       
    СтруктураОбъекта = Новый Структура();
	СтруктураОбъекта.Вставить("Object_ID", ПредставлениеСсылки);
	СтруктураОбъекта.Вставить("Metadata_FullName", МетаданныеОбъекта.ПолноеИмя());
	СтруктураОбъекта.Вставить("Metadata_Name", МетаданныеОбъекта.Имя);
	СтруктураОбъекта.Вставить("Object_Version", Объект.ВерсияДанных);    
	СтруктураОбъекта.Вставить("Object_DateTime", ДатаРегистрации);
	СтруктураОбъекта.Вставить("Object_User", ПредставлениеПользователя);
    СтруктураОбъекта.Вставить("Object_User_1C", ПредставлениеПользователя1С);
    СтруктураОбъекта.Вставить("Host_1С", ИмяСервераСеанса); 
    
    СообщенияОтправки = md_ВерсионированиеElasticsearchСлужебный.СообщенияОтправкиПоПараметрам(Объект, 
    СтруктураОбъекта, СтруктураОбработкиES.ПараметрыВерсионирования);

    СтруктураОбработкиES.Вставить("СообщенияОтправки", СообщенияОтправки);
    
КонецПроцедуры

#КонецОбласти

